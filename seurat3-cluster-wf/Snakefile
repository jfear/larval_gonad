from pathlib import Path

import pandas as pd

from larval_gonad.config import read_config

common_config = read_config("../config/common.yaml")
colors_config = read_config("../config/colors.yaml")
ASSEMBLY = common_config["assembly"]
TAG = common_config["tag"]
SAMPLES = ["testis1", "testis2", "testis3", "testis4"]


rule targets:
    input: 
        "../output/seurat3-cluster-wf/testis1_individual.html",
        "../output/seurat3-cluster-wf/testis2_individual.html",
        "../output/seurat3-cluster-wf/testis3_individual.html",
        "../output/seurat3-cluster-wf/testis4_individual.html",
        "../output/seurat3-cluster-wf/raw_by_cluster_rep.feather",
        "../output/seurat3-cluster-wf/tpm_by_cluster_rep.feather",
        "../output/seurat3-cluster-wf/zscore_by_cluster_rep.feather",
        "../output/seurat3-cluster-wf/zscore_by_cell.feather",
        expand("../output/seurat3-cluster-wf/combined_n{cnt}_biomarkers.feather", cnt=[3, 4]),
        expand(
            "../output/seurat3-cluster-wf/germline_deg/{comparison}.feather",
            comparison=[
                "GvPS", "GvEPS", "GvMLPS", "GvMPS", "GvLPS",
                "EPSvMLPS", "EPSvMPS", "EPSvLPS",
                "MPSvLPS"
            ]
        ),
        expand(
            "../output/seurat3-cluster-wf/{sample}_{ftypes}.feather", 
            sample=[
                "testis1_individual",
                "testis2_individual",
                "testis3_individual",
                "testis4_individual",
                "combined_n3",
                "combined_n4",
            ],
            ftypes=[
                "normalized",
                "metadata",
                "pca_cell_embedings",
                "pca_gene_loadings",
                "umap",
            ]
        ),
        "../output/seurat3-cluster-wf/germline_deg/GvLPS_G_biased.pkl",
        "../output/seurat3-cluster-wf/germline_deg/GvLPS_LPS_biased.pkl",
        "../output/seurat3-cluster-wf/germline_deg/GvLPS_NS_biased.pkl",
        "../output/seurat3-cluster-wf/combined_n3_clusters.feather",
        "../output/seurat3-cluster-wf/combined_n3_GO_analysis.html",
        "../output/seurat3-cluster-wf/combined_n3_figures/umap_panel.svg",
        "../output/seurat3-cluster-wf/combined_n3_figures/gene_projections/done",
        "../output/seurat3-cluster-wf/combined_n3_figures/expression_patterns/done",
        "../output/seurat3-cluster-wf/combined_n3_figures/all_biomarkers.svg",
        "../output/seurat3-cluster-wf/combined_n3_figures/unique_biomarkers.svg",
        "../output/seurat3-cluster-wf/combined_n3_figures/multi_biomarkers.svg",
        "../output/seurat3-cluster-wf/combined_n3_figures/barplot-cell-per-cluster.svg",
        "../output/seurat3-cluster-wf/combined_n3_figures/boxplot-total-umi.svg",
        "../output/seurat3-cluster-wf/combined_n3_figures/boxplot-total-umi_y_genes.svg",


################################################################################
# Clustering
################################################################################

## Individual Samples
rule testis1:
    input:
        rmd = "scripts/testis1_individual.Rmd"
    output: 
        html = "../output/seurat3-cluster-wf/testis1_individual.html",
    conda: "../envs/seurat3.yaml"
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 16,
        time_hr=lambda wildcards, attempt: attempt * 6
    script: "../scripts/rscript.py"


rule testis2:
    input:
        rmd = "scripts/testis2_individual.Rmd"
    output: 
        html = "../output/seurat3-cluster-wf/testis2_individual.html",
    conda: "../envs/seurat3.yaml"
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 16,
        time_hr=lambda wildcards, attempt: attempt * 6
    script: "../scripts/rscript.py"


rule testis3:
    input:
        rmd = "scripts/testis3_individual.Rmd"
    output: 
        html = "../output/seurat3-cluster-wf/testis3_individual.html",
    conda: "../envs/seurat3.yaml"
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 16,
        time_hr=lambda wildcards, attempt: attempt * 6
    script: "../scripts/rscript.py"


rule testis4:
    input:
        rmd = "scripts/testis4_individual.Rmd"
    output: 
        html = "../output/seurat3-cluster-wf/testis4_individual.html",
    conda: "../envs/seurat3.yaml"
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 16,
        time_hr=lambda wildcards, attempt: attempt * 6
    script: "../scripts/rscript.py"


## Merged Samples
rule merge_3reps:
    input: 
        rmd = "scripts/combined_n3.Rmd",
        data = expand("../output/seurat3-cluster-wf/{sample}_individual.html", sample=SAMPLES[:3])
    output:
        html = "../output/seurat3-cluster-wf/combined_n3.html",
    conda: "../envs/seurat3.yaml"
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 16,
        time_hr=lambda wildcards, attempt: attempt * 6
    script: "../scripts/rscript.py"


rule merge_4reps:
    input: 
        rmd = "scripts/combined_n4.Rmd",
        data = expand("../output/seurat3-cluster-wf/{sample}_individual.html", sample=SAMPLES)
    output:
        html = "../output/seurat3-cluster-wf/combined_n4.html",
    conda: "../envs/seurat3.yaml"
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 16,
        time_hr=lambda wildcards, attempt: attempt * 6
    script: "../scripts/rscript.py"


def _params(wildcards):
    return {
        "testis1_individual": "RNA_snn_res.0.8",
        "testis2_individual": "RNA_snn_res.0.6",
        "testis3_individual": "RNA_snn_res.0.6",
        "testis4_individual": "RNA_snn_res.0.6",
        "combined_n3": "integrated_snn_res.0.3",
        "combined_n4": "integrated_snn_res.0.3",

    }[wildcards.sample]


rule dump_robj:
    input: 
        html = "../output/seurat3-cluster-wf/{sample}.html",
        gene_annotation = f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather"
    output: 
        norm = "../output/seurat3-cluster-wf/{sample}_normalized.feather",
        metadata = "../output/seurat3-cluster-wf/{sample}_metadata.feather",
        pca_embed = "../output/seurat3-cluster-wf/{sample}_pca_cell_embedings.feather",
        pca_load = "../output/seurat3-cluster-wf/{sample}_pca_gene_loadings.feather",
        umap = "../output/seurat3-cluster-wf/{sample}_umap.feather",
    params:
        integrated_resolution = _params
    conda: "../envs/seurat3.yaml"
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 16,
        time_hr=lambda wildcards, attempt: attempt * 2
    script: "scripts/dump.R"


rule combined_n3_clusters:
    input: "../output/seurat3-cluster-wf/combined_n3_metadata.feather"
    output: "../output/seurat3-cluster-wf/combined_n3_clusters.feather"
    params: 
        cluster_annot = common_config['cluster_annot'],
        cluster_order = common_config['cluster_order']
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 8,
        time_hr=lambda wildcards, attempt: attempt * 2
    run: 
        (
            pd.read_feather(input[0], columns=['cell_id', 'cluster'])
            .assign(
                cluster = lambda x: pd.Categorical(
                    x.cluster.map(params['cluster_annot']), ordered=True, categories=params['cluster_order']
                )
            )
            .assign(rep = lambda x: x.cell_id.str.extract("(rep\d)_.*", expand=False))
            .to_feather(output[0])
        )


rule combined_n3_GO:
    input: 
        rmd = "scripts/combined_n3_GO_analysis.Rmd",
        data = "../output/seurat3-cluster-wf/combined_n3_biomarkers.feather"
    output:
        html = "../output/seurat3-cluster-wf/combined_n3_GO_analysis.html",
    conda: "../envs/go_analysis.yaml"
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 16,
        time_hr=lambda wildcards, attempt: attempt * 6
    script: "../scripts/rscript.py"


################################################################################
# Differential Expression
################################################################################
rule biomarkers:
    input: 
        html = "../output/seurat3-cluster-wf/combined_n{cnt}.html",
        gene_annotation = f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather"
    output: 
        tsv = "../output/seurat3-cluster-wf/combined_n{cnt}_biomarkers.tsv",
        feather = "../output/seurat3-cluster-wf/combined_n{cnt}_biomarkers.feather"
    conda: "../envs/seurat3.yaml"
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 16,
        time_hr=lambda wildcards, attempt: attempt * 6
    script: "scripts/combined_biomarkers.R"


rule germline_deg:
    input: 
        html = "../output/seurat3-cluster-wf/combined_n3.html",
        gene_annotation = f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather"
    output: 
        # SP vs EPS & MPS & LPS
        gvps = "../output/seurat3-cluster-wf/germline_deg/GvPS.feather",
        # SP vs EPS
        gveps = "../output/seurat3-cluster-wf/germline_deg/GvEPS.feather",
        # SP vs MPS & LPS
        gvmlps = "../output/seurat3-cluster-wf/germline_deg/GvMLPS.feather",
        # SP vs MPS
        gvmps = "../output/seurat3-cluster-wf/germline_deg/GvMPS.feather",
        # SP vs LPS
        gvlps = "../output/seurat3-cluster-wf/germline_deg/GvLPS.feather",
        # EPS vs MPS & LPS
        epsvmlps = "../output/seurat3-cluster-wf/germline_deg/EPSvMLPS.feather",
        # EPS vs MPS
        epsvmps = "../output/seurat3-cluster-wf/germline_deg/EPSvMPS.feather",
        # EPS vs LPS
        epsvlps = "../output/seurat3-cluster-wf/germline_deg/EPSvLPS.feather",
        # MPS vs LPS
        mpsvlps = "../output/seurat3-cluster-wf/germline_deg/MPSvLPS.feather",
    conda: "../envs/seurat3.yaml"
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 16,
        time_hr=lambda wildcards, attempt: attempt * 6
    script: "scripts/germline_deg.R"


rule sp_v_lps_deg_gene_lists:
    input: rules.germline_deg.output.gvlps.format(cnt=3)
    output: 
        up="../output/seurat3-cluster-wf/germline_deg/GvLPS_G_biased.pkl",
        down="../output/seurat3-cluster-wf/germline_deg/GvLPS_LPS_biased.pkl",
        ns="../output/seurat3-cluster-wf/germline_deg/GvLPS_NS_biased.pkl",
    run:
        from larval_gonad.io import pickle_dump
        df = pd.read_feather(input[0]).query("p_val_adj <= 0.01")
        pickle_dump(df.query("avg_logFC > 0").FBgn.tolist(), output.up)
        pickle_dump(df.query("avg_logFC < 0").FBgn.tolist(), output.down)
        pickle_dump(pd.read_feather(input[0]).query("p_val_adj > 0.01").FBgn.tolist(), output.ns)



################################################################################
# Aggregate and Normalize Counts
################################################################################
rule raw_agg:
    input:
        raw="../output/cellselection-wf/raw.feather",
        clusters=rules.combined_n3_clusters.output[0]
    output: "../output/seurat3-cluster-wf/raw_by_cluster_rep.feather"
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 12,
        time_hr=lambda wildcards, attempt: attempt * 2
    script: "scripts/raw_agg.py"


rule tpm:
    input:
        raw_agg = rules.raw_agg.output[0],
        gene_metadata = f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather"
    output: "../output/seurat3-cluster-wf/tpm_by_cluster_rep.feather"
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 12,
        time_hr=lambda wildcards, attempt: attempt * 2
    script: "scripts/tpm.py"


rule zscore:
    input: rules.tpm.output[0]
    output: "../output/seurat3-cluster-wf/zscore_by_cluster_rep.feather"
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 12,
        time_hr=lambda wildcards, attempt: attempt * 2
    script: "scripts/zscore.py"


rule cell_zscore:
    input: rules.dump_robj.output.norm.format(sample="combined_n3")
    output: "../output/seurat3-cluster-wf/zscore_by_cell.feather"
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 12,
        time_hr=lambda wildcards, attempt: attempt * 2
    script: "scripts/cell_zscore.py"


################################################################################
# Plots
################################################################################
rule plot_barplot_cell_per_cluster_number:
    input: rules.combined_n3_clusters.output[0]
    output: "../output/seurat3-cluster-wf/combined_n3_figures/barplot-cell-per-cluster.svg"
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 8,
        time_hr=lambda wildcards, attempt: attempt * 2
    script: "scripts/barplot_cell_per_cluster.py"


rule plot_umap:
    input:
        umap=rules.dump_robj.output.umap,
        clusters=rules.combined_n3_clusters.output[0]
    output: "../output/seurat3-cluster-wf/{sample}_figures/umap_panel.svg",
    params: colors = colors_config["clusters"],
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 8,
        time_hr=lambda wildcards, attempt: attempt * 2
    script: "scripts/plot_umap.py"
    

rule feature_plot:
    input: 
        zscores=rules.cell_zscore.output[0],
        umap=rules.dump_robj.output.umap.format(sample="combined_n3"),
        gene_annot=f"../references/gene_annotation_dmel_{TAG}.feather"
    params: 
        pattern=lambda wildcards: "../output/seurat3-cluster-wf/combined_n3_figures/gene_projections/{symbol}_{FBgn}.svg"
    output: "../output/seurat3-cluster-wf/combined_n3_figures/gene_projections/done"
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 8,
        time_hr=lambda wildcards, attempt: attempt * 2
    script: "scripts/feature_plot.py"


rule lit_gene_expression_patterns:
    input: 
        norm=rules.dump_robj.output.norm.format(sample="combined_n3"),
        clusters=rules.combined_n3_clusters.output[0]
    params: pattern=lambda wildcards: "../output/seurat3-cluster-wf/combined_n3_figures/expression_patterns/{symbol}_{FBgn}.svg"
    output: "../output/seurat3-cluster-wf/combined_n3_figures/expression_patterns/done"
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 8,
        time_hr=lambda wildcards, attempt: attempt * 2
    script: "scripts/expression_patterns.py"


rule all_biomarkers:
    input: 
        biomarkers=rules.biomarkers.output.feather.format(cnt="3"),
        clusters=rules.combined_n3_clusters.output[0],
        zscore=rules.zscore.output[0],
        lit_genes="../config/literature_genes.yaml"
    params: 
        color=colors_config['heatmap'],
        cluster_annot=common_config['cluster_annot'],
        cluster_order=common_config['cluster_order'],
    output: "../output/seurat3-cluster-wf/combined_n3_figures/all_biomarkers.svg"
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 8,
        time_hr=lambda wildcards, attempt: attempt * 2
    script: "scripts/all_biomarkers.py"


rule unique_biomarkers:
    input: 
        biomarkers=rules.biomarkers.output.feather.format(cnt="3"),
        clusters=rules.combined_n3_clusters.output[0],
        zscore=rules.zscore.output[0],
        lit_genes="../config/literature_genes.yaml"
    params: 
        color=colors_config['heatmap'],
        cluster_annot=common_config['cluster_annot'],
        cluster_order=common_config['cluster_order'],
    output: "../output/seurat3-cluster-wf/combined_n3_figures/unique_biomarkers.svg"
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 8,
        time_hr=lambda wildcards, attempt: attempt * 2
    script: "scripts/unique_biomarkers.py"


rule multi_biomarkers:
    input: 
        biomarkers=rules.biomarkers.output.feather.format(cnt="3"),
        clusters=rules.combined_n3_clusters.output[0],
        zscore=rules.zscore.output[0],
        lit_genes="../config/literature_genes.yaml"
    params: 
        color=colors_config['heatmap'],
        cluster_annot=common_config['cluster_annot'],
        cluster_order=common_config['cluster_order'],
    output: "../output/seurat3-cluster-wf/combined_n3_figures/multi_biomarkers.svg"
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 8,
        time_hr=lambda wildcards, attempt: attempt * 2
    script: "scripts/multi_biomarkers.py"


rule boxplot_total_umi:
    input: rules.raw_agg.output[0]
    output: "../output/seurat3-cluster-wf/combined_n3_figures/boxplot-total-umi.svg"
    params: cluster_colors=colors_config["clusters"]
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 8,
        time_hr=lambda wildcards, attempt: attempt * 2
    script: "scripts/boxplot_total_umi.py"


def _y_genes(wildcards):
    return (
        pd.read_feather(f"../references/gene_annotation_dmel_{TAG}.feather")
        .query("FB_chrom == 'Y'")
        .FBgn.tolist()
    )


rule boxplot_total_umi_y_genes:
    input: rules.raw_agg.output[0]
    output: "../output/seurat3-cluster-wf/combined_n3_figures/boxplot-total-umi_y_genes.svg"
    params: 
        cluster_colors=colors_config["clusters"],
        fbgns=_y_genes
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 8,
        time_hr=lambda wildcards, attempt: attempt * 2
    script: "scripts/boxplot_total_umi.py"
