"""Scrip to generate figures and needed material for the Science Submssion.

"""
from pathlib import Path

workdir: '.'
configfile: 'config.yaml'


rule all:
    input:
        'data/clusters.parquet',
        'data/fbgn2symbol.pkl',
        'data/symbol2fbgn.pkl',
        expand('figures/{fig}.svg', fig=['fig1']),
#         expand('figures/{fig}.png', fig=['fig1']), expand('figures/{fig}.pdf', fig=['fig1'])


rule clusters:
    input: Path(config['seurat_dir'], 'clusters.tsv').as_posix()
    params:
        resolution=config['resolution'],
    output: 'data/clusters.parquet'
    script: 'scripts/parse_clusters.py'


rule tsne:
    input: Path(config['seurat_dir'], 'tsne.tsv').as_posix()
    output: 'data/tsne.parquet'
    run:
        import pandas as pd
        df = pd.read_csv(input[0], sep='\t', index_col=0)
        df.to_parquet(output[0])


rule fbgn2symbol:
    output: 'data/fbgn2symbol.pkl'
    script: 'scripts/parse_fbgn2symbol.py'


rule symbol2fbgn:
    input: rules.fbgn2symbol.output[0]
    output: 'data/symbol2fbgn.pkl'
    script: 'scripts/convert_fbgn2symbol_to_symbol2fbgn.py'


rule ptrap_scores:
    output: 'data/ptrap_scores.parquet'
    script: 'scripts/ptrap-scores.py'


rule plot_tsne:
    input: rules.tsne.output[0], rules.clusters.output[0]
    output: 'figures/tsne.svg'
    script: 'scripts/plot_tsne.py'


rule plot_heatmap_all_genes:
    input: '../scrnaseq-wf/data/tpm_zscore.parquet'
    output: 'figures/heatmap-all-genes.svg'
    script: 'scripts/plot_heatmap_all_genes.py'


rule plot_heatmap_lit_genes:
    input: '../scrnaseq-wf/data/tpm_zscore.parquet'
    output: 'figures/heatmap-lit-genes.svg'
    script: 'scripts/plot_heatmap_lit_genes.py'


rule plot_heatmap_ptrap_genes:
    input: '../scrnaseq-wf/data/tpm_zscore.parquet', rules.ptrap_scores.output[0]
    output: 'figures/heatmap-ptrap-genes.svg'
    script: 'scripts/plot_heatmap_ptrap_genes.py'


rule fig1:
    input:
        tsne=rules.plot_tsne.output[0],
        hmap_all=rules.plot_heatmap_all_genes.output[0],
        hmap_lit=rules.plot_heatmap_lit_genes.output[0],
        hmap_ptrap=rules.plot_heatmap_ptrap_genes.output[0],
    output: 'figures/fig1.svg'
    script: 'scripts/fig1.py'


rule svg_png:
    input: '{fig}.svg'
    output: '{fig}.png'
    shell: "inkscape {input[0]} --export-png={output[0]} --export-dpi=800 --export-background='white' --without-gui"

rule svg_pdf:
    input: '{fig}.svg'
    output: '{fig}.pdf'
    shell: "inkscape {input[0]} --export-pdf={output[0]}"


# vim: set ft=snakemake.python
