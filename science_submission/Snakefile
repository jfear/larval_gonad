"""Scrip to generate figures and needed material for the Science Submssion"""
import os
import yaml
from pathlib import Path

import pandas as pd

from larval_gonad.config import read_config
from larval_gonad.io import safe_gene_name

workdir: "."
configfile: "../config/common.yaml"
colors_config = read_config("../config/colors.yaml")

SAMPLES = ["testis1", "testis2", "testis3"]

REFERENCES_DIR = os.environ.get("REFERENCES_DIR", config.get("REFERENCES_DIR", ""))
ASSEMBLY = config["assembly"]
TAG = config["tag"]

LIT_GENES = read_config("../config/literature_genes.yaml")
PTRAP_GENES = read_config("../config/ptrap_genes.yaml")
COLORS = yaml.full_load(open("../config/colors.yaml"))

FBGN2SYMBOL = (
    pd.read_feather("../references/gene_annotation_dmel_r6-26.feather", columns=["FBgn", "gene_symbol"])
    .set_index("FBgn")
    .squeeze()
    .reindex(pd.read_feather("../output/seurat3-cluster-wf/combined_n3_normalized.feather", columns=["FBgn"]).FBgn.unique().tolist())
    .map(safe_gene_name)
)

wildcard_constraints: 
    sample="testis\d"

rule all:
    input: 
        # Figures
        ## Fig 1 + Supplement
        "../output/science_submission/figures/cluster-legend.svg",
        "../output/science_submission/figures/lit-gene-evidence.svg",
        "../output/science_submission/figures/lit-gene-evidence_soma.svg",
        "../output/science_submission/figures/literature_expression_panel.done",
        # expand("../output/science_submission/figures/projections_all_genes/{fbgn}_{symbol}.svg", zip, fbgn=FBGN2SYMBOL.index.tolist(), symbol=FBGN2SYMBOL.values.tolist()),   ## commented out to spead up build snakemake.
        ## Fig 2 + Supplement
        "../output/science_submission/figures/fig2.svg",
        "../output/science_submission/figures/fig2_boxplot.svg",
        ## Fig 3 + Supplement
        "../output/science_submission/figures/fig3.svg",
        # Tables
        "../output/science_submission/cell_metadata.tsv",
        "../output/science_submission/gene_metadata.tsv",
        "../output/science_submission/cluster_metadata.tsv",
        '../output/science_submission/scRNAseq_raw_counts.tsv.gz',
        '../output/science_submission/scRNAseq_seurat_normalized_counts.tsv.gz',
        "../output/science_submission/biomarkers.tsv",
        '../output/science_submission/adult_bulk_RNA-Seq_gene_level_counts.tsv.gz',
        '../output/science_submission/larval_bulk_RNA-Seq_gene_level_counts.tsv.gz',
        '../output/science_submission/larval_bulk_RNA-Seq_intergenic_counts.tsv.gz',
        "../output/science_submission/spearman_corr.tsv",
        expand("../output/science_submission/{comparison}.tsv", comparison=["EPSvLPS", "EPSvMLPS", "EPSvMPS", "GvEPS", "GvLPS", "GvMLPS", "GvMPS", "GvPS", "MPSvLPS"]),

################################################################################
# Figure 1: scRNA-Seq intro, cluster, and annotate
################################################################################
rule plot_cluster_legend:
    params:
        cluster_colors=COLORS["clusters"],
        legend_names=config["legend_names"]
    output: "../output/science_submission/figures/cluster-legend.svg"
    script: "scripts/plot_cluster_legend.py"


rule plot_literature_panel:
    input:
        gene_annot=f"../references/gene_annotation_dmel_{TAG}.feather",
        clusters="../output/seurat3-cluster-wf/combined_n3_clusters.feather",
        zscores="../output/seurat3-cluster-wf/zscore_by_cell.feather",
        umap="../output/seurat3-cluster-wf/combined_n3_umap.feather",
        norm="../output/seurat3-cluster-wf/combined_n3_normalized.feather"
    output: "../output/science_submission/figures/literature_expression_panel.done"
    params: pattern=lambda wildcards: "../output/science_submission/figures/literature_expression_panel_{cnt}.svg"
    script: "scripts/literature_gene_expression_panel.py"


rule plot_lit_gene_evidence:
    input:
         gene_metadata = f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather",
         lit_evidence = '../data/external/miriam/lit_gene_dummy_vars.tsv',
         zscore_by_cluster_rep="../output/seurat3-cluster-wf/zscore_by_cluster_rep.feather"
    output: "../output/science_submission/figures/lit-gene-evidence.svg"
    params: cmap=COLORS["heatmap"]
    script: "scripts/plot_lit_gene_evidence.py"


rule plot_lit_gene_evidence_soma:
    input:
         gene_metadata = f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather",
         lit_evidence = '../data/external/miriam/lit_gene_dummy_vars.tsv',
         zscore_by_cluster_rep="../output/seurat3-cluster-wf/zscore_by_cluster_rep.feather"
    output: "../output/science_submission/figures/lit-gene-evidence_soma.svg"
    params: cmap=COLORS["heatmap"]
    script: "scripts/plot_lit_gene_evidence_soma.py"


rule plot_umaps_all_genes:
    input:
        gene_annot=f"../references/gene_annotation_dmel_{TAG}.feather",
        clusters="../output/seurat3-cluster-wf/combined_n3_clusters.feather",
        zscores="../output/seurat3-cluster-wf/zscore_by_cell.feather",
        umap="../output/seurat3-cluster-wf/combined_n3_umap.feather",
        norm="../output/seurat3-cluster-wf/combined_n3_normalized.feather"
    output: "../output/science_submission/figures/projections_all_genes/{fbgn}_{symbol}.svg"
    script: "scripts/plot_umap_projections.py"


################################################################################
# Figure 2: Chromosome Expression and Gene Counts
################################################################################
rule data_fig2:
    input: 
        gene_annot=f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather",
        chrom_sizes=f"../lcdb-references/{ASSEMBLY}/{TAG}/fasta/{ASSEMBLY}_{TAG}.chromsizes",
        adult_bulk="../output/expression-atlas-wf/w1118_gene_counts.feather",
        L3_bulk="../output/bulk2-rnaseq-wf/testis_ovary_counts.feather",
        L3_sc_agg="../output/seurat3-cluster-wf/aggegated_gene_counts.feather",
        L3_sc_by_class="../output/seurat3-cluster-wf/aggegated_gene_counts_by_germ_soma.feather",
    output: 
        prop_reads="../output/science_submission/fig2_data_prop_reads.feather",
        pct_expressed="../output/science_submission/fig2_data_pct_expressed.feather"
    script: "scripts/fig2_data.py"


rule plot_fig2:
    input: 
        prop_reads=rules.data_fig2.output.prop_reads,
        pct_expressed=rules.data_fig2.output.pct_expressed
    output: "../output/science_submission/figures/fig2.svg"
    params: colors=COLORS
    script: "scripts/plot_fig2.py"


rule plot_fig2_boxplot:
    input: 
        gene_annot="../references/gene_annotation_dmel_r6-26.feather",
        adult="../output/expression-atlas-wf/w1118_gene_counts.feather",
        larval="../output/bulk2-rnaseq-wf/rnaseq_aggregation/tpm_gene_level_counts.tsv",
    output: "../output/science_submission/figures/fig2_boxplot.svg"
    params: colors=COLORS
    script: "scripts/plot_fig2_boxplots.py"


################################################################################
# Figure 3: X + 4 Inactivation
################################################################################
rule plot_fig3:
    input: 
        gene_annot=f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather",
        all='../output/x-to-a-wf/db/expressed.dat',
        common='../output/x-to-a-wf/db/commonly_expressed.dat',
        tpm='../output/seurat3-cluster-wf/tpm_by_cluster.feather',
        common_fbgns="../output/cellselection-wf/commonly_expressed_genes.pkl",
    params:
          cluster_color=COLORS["clusters"],
          cluster_order=config['cluster_order'],
    output: "../output/science_submission/figures/fig3.svg"
    script: "scripts/fig3.py"


################################################################################
# Tables
################################################################################
rule cell_metadata:
    input: 
        metadata="../output/cellselection-wf/cell_metadata.feather",
        clusters="../output/seurat3-cluster-wf/combined_n3_clusters.feather",
        cell_calls=expand("../output/cellselection-wf/{sample}_combined_cell_calls.feather", sample=["testis1", "testis2", "testis3"]),
        scrublets=expand("../output/cellselection-wf/{sample}_scrublet_dublets.txt", sample=["testis1", "testis2", "testis3"]),
        umap="../output/seurat3-cluster-wf/combined_n3_umap.feather",
        autosome_ratios_all_genes="../output/x-to-a-wf/autosome_ratios_expressed_by_cell.feather",
        autosome_ratios_commonly_expressed_genes="../output/x-to-a-wf/autosome_ratios_commonly_expressed_by_cell.feather",
    output: "../output/science_submission/cell_metadata.tsv"
    script: "scripts/cell_metadata_table.py"


rule gene_metadata:
    input: 
        gene_annot="../references/gene_annotation_dmel_r6-26.feather",
        biomarkers="../output/seurat3-cluster-wf/combined_n3_biomarkers.feather",
        raw="../output/seurat3-cluster-wf/raw_by_cluster_rep.feather",
        tpm="../output/seurat3-cluster-wf/tpm_by_cluster_rep.feather",
        zscore="../output/seurat3-cluster-wf/zscore_by_cluster_rep.feather",
    params: 
        cluster_annot=config['cluster_annot'],
        cluster_order=config['cluster_order'],
    output: "../output/science_submission/gene_metadata.tsv"
    script: "scripts/gene_metadata_table.py"


rule cluster_metadata:
    input:
        clusters="../output/seurat3-cluster-wf/combined_n3_clusters.feather",
        tpm="../output/seurat3-cluster-wf/tpm_by_cluster.feather",
    params:
        cluster_annot=config["cluster_annot"],
        cluster_order=config["cluster_order"],
        legend_names=config["legend_names"],
    output: "../output/science_submission/cluster_metadata.tsv"
    script: "scripts/cluster_metadata_table.py"


rule raw_to_tsv:
    input: '../output/cellselection-wf/raw.feather'
    output: '../output/science_submission/scRNAseq_raw_counts.tsv.gz'
    run:
        pd.read_feather(input[0]).to_csv(output[0], index=False, sep="\t")


rule seurat_normalized_to_tsv:
    input: '../output/seurat3-cluster-wf/combined_n3_normalized.feather'
    output: '../output/science_submission/scRNAseq_seurat_normalized_counts.tsv.gz'
    run:
        pd.read_feather(input[0]).drop("gene_symbol", axis=1).to_csv(output[0], index=False, sep="\t")


rule biomarkers_to_tsv:
    input: "../output/seurat3-cluster-wf/combined_n3_biomarkers.feather"
    output: "../output/science_submission/biomarkers.tsv"
    params: cluster_annot = config["cluster_annot"]
    run:
        (
            pd.read_feather(input[0])
            .assign(cluster=lambda x: x.cluster.map(lambda y: params.cluster_annot[y]))
            .to_csv(output[0], index=False, sep="\t")
        )
    

rule deg_to_tsv:
    input: "../output/seurat3-cluster-wf/germline_deg/{comparison}.feather"
    output: "../output/science_submission/{comparison}.tsv"
    run:
        pd.read_feather(input[0]).to_csv(output[0], index=False, sep="\t")


rule larval_bulk_gene_level:
    input: "../output/bulk2-rnaseq-wf/rnaseq_aggregation/gene_level_counts.tsv"
    output: '../output/science_submission/larval_bulk_RNA-Seq_gene_level_counts.tsv.gz'
    shell: "gzip -c {input[0]} > {output[0]}"


rule larval_bulk_intergenic_level:
    input: "../output/bulk2-rnaseq-wf/rnaseq_aggregation/intergenic_counts.tsv"
    output: '../output/science_submission/larval_bulk_RNA-Seq_intergenic_counts.tsv.gz'
    shell: "gzip -c {input[0]} > {output[0]}"


rule adult_bulk_gene_level:
    input: "../output/expression-atlas-wf/w1118_gene_counts.feather"
    output: '../output/science_submission/adult_bulk_RNA-Seq_gene_level_counts.tsv.gz'
    run: 
        pd.pivot_table(
            pd.read_feather(input[0], columns=["FBgn", "sample_ID", "Count"]),
            index="FBgn",
            columns="sample_ID",
            values="Count"
        ).to_csv(output[0], sep="\t")
        

rule spearman_corr_bulk_vs_sum_single_cell:
    input:
        gene_annot="../references/gene_annotation_dmel_r6-26.feather",
        larval_scrnaseq="../output/cellselection-wf/raw.feather",
        larval_bulk="../output/bulk2-rnaseq-wf/rnaseq_aggregation/tpm_gene_level_counts.tsv",
    output: "../output/science_submission/spearman_corr_bulk_vs_sum_single_cell.tsv"
    script: "scripts/correlation_table_bulk_sum_single_cell.py"

rule spearman_corr:
    input:
        gene_annot="../references/gene_annotation_dmel_r6-26.feather",
        larval_scrnaseq="../output/seurat3-cluster-wf/tpm_by_cluster_rep.feather",
        larval_bulk="../output/bulk2-rnaseq-wf/rnaseq_aggregation/tpm_gene_level_counts.tsv",
        adult_bulk="../output/expression-atlas-wf/w1118_gene_counts.feather",
    output: "../output/science_submission/spearman_corr.tsv"
    script: "scripts/correlation_table.py"

