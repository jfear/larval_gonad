"""Scrip to generate figures and needed material for the Science Submssion"""
import os
import yaml
from pathlib import Path

import pandas as pd

from larval_gonad.config import read_config

workdir: "."
configfile: "../config/common.yaml"
colors_config = read_config("../config/colors.yaml")

SAMPLES = ["testis1", "testis2", "testis3"]

REFERENCES_DIR = os.environ.get("REFERENCES_DIR", config.get("REFERENCES_DIR", ""))
ASSEMBLY = config["assembly"]
TAG = config["tag"]

LIT_GENES = read_config("../config/literature_genes.yaml")
PTRAP_GENES = read_config("../config/ptrap_genes.yaml")
COLORS = yaml.full_load(open("../config/colors.yaml"))

wildcard_constraints: 
    sample="testis\d"

rule all:
    input: 
        # expand("../output/science_submission/figures/lit_genes_umap_panel_{group}.svg", group=LIT_GENES.keys()),
        # expand("../output/science_submission/figures/ptrap_genes_umap_panel_{group}.svg", group=PTRAP_GENES.keys()),
        # expand("../output/science_submission/figures/lit_genes_expression_patterns_panel_{group}.svg", group=LIT_GENES.keys()),
        # expand("../output/science_submission/figures/ptrap_genes_expression_patterns_panel_{group}.svg", group=PTRAP_GENES.keys()),
        "../output/science_submission/figures/cluster-legend.svg",
        "../output/science_submission/figures/lit-gene-evidence.svg",
        "../output/science_submission/figures/lit-gene-evidence_soma.svg",
        expand(
            "../output/science_submission/figures/GvLPS_{direction}_biased_chrom_location.svg",
            direction=['G', 'LPS']
        ),
        expand(
            "../output/science_submission/figures/{clus}_chrom_enrichment.svg",
            clus=["G", "EPS", "MPS", "LPS"]
        ),
        expand(
            "../output/science_submission/figures/GvLPS_{direction}_expression_by_chrom.svg",
            direction=['G', 'LPS']
        ),
        "../output/science_submission/biomarkers.tsv",
        "../output/science_submission/figures/ptrap_expression_panel.svg",
        "../output/science_submission/figures/chrom_read_count_panel_scale_scaffold_len.svg",
        "../output/science_submission/figures/chrom_read_count_panel_scale_num_genes_per_scaffold.svg",
        "../output/science_submission/figures/chrom_read_count_panel_scale_num_expressed_genes_per_scaffold.svg",
        "../output/science_submission/figures/chrom_prop_expressed_all_genes.svg",
        "../output/science_submission/figures/chrom_prop_expressed_genes_in_experiment.svg",
        "../output/science_submission/figures/fig2.svg",
        "../output/science_submission/figures/fig3.svg",
        "../output/science_submission/figures/literature_expression_panel.done",
        "../output/science_submission/cell_metadata.tsv",
        "../output/science_submission/gene_metadata.tsv",


################################################################################
# Figure 1: scRNA-Seq intron, cluster, annotate, and validate
################################################################################
rule plot_cluster_legend:
    params:
        cluster_colors=COLORS["clusters"],
        legend_names=config["legend_names"]
    output: "../output/science_submission/figures/cluster-legend.svg"
    script: "scripts/plot_cluster_legend.py"


rule lit_gene_umap_panel:
    input: f"../references/gene_annotation_dmel_{TAG}.feather"
    params: 
        pattern=lambda wildcards: "../output/seurat3-cluster-wf/combined_n3_figures/gene_projections/{symbol}_{FBgn}.svg",
        genes=lambda wildcards: LIT_GENES[wildcards.group]
    output: "../output/science_submission/figures/lit_genes_umap_panel_{group}.svg"
    script: "scripts/gene_panels.py"


rule lit_gene_expression_patterns_panel:
    input: f"../references/gene_annotation_dmel_{TAG}.feather"
    params: 
        pattern=lambda wildcards: "../output/seurat3-cluster-wf/combined_n3_figures/expression_patterns/{symbol}_{FBgn}.svg",
        genes=lambda wildcards: LIT_GENES[wildcards.group]
    output: "../output/science_submission/figures/lit_genes_expression_patterns_panel_{group}.svg"
    script: "scripts/gene_panels.py"


rule ptrap_gene_umap_panel:
    input: f"../references/gene_annotation_dmel_{TAG}.feather"
    params: 
        pattern=lambda wildcards: "../output/seurat3-cluster-wf/combined_n3_figures/gene_projections/{symbol}_{FBgn}.svg",
        genes=lambda wildcards: PTRAP_GENES[wildcards.group]
    output: "../output/science_submission/figures/ptrap_genes_umap_panel_{group}.svg"
    script: "scripts/gene_panels.py"


rule ptrap_gene_expression_patterns_panel:
    input: f"../references/gene_annotation_dmel_{TAG}.feather"
    params: 
        pattern=lambda wildcards: "../output/seurat3-cluster-wf/combined_n3_figures/expression_patterns/{symbol}_{FBgn}.svg",
        genes=lambda wildcards: PTRAP_GENES[wildcards.group]
    output: "../output/science_submission/figures/ptrap_genes_expression_patterns_panel_{group}.svg"
    script: "scripts/gene_panels.py"


rule plot_lit_gene_evidence:
    input:
         gene_metadata = f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather",
         lit_evidence = '../data/external/miriam/lit_gene_dummy_vars.tsv',
         zscore_by_cluster_rep="../output/seurat3-cluster-wf/zscore_by_cluster_rep.feather"
    output: "../output/science_submission/figures/lit-gene-evidence.svg"
    params: cmap=COLORS["heatmap"]
    script: "scripts/plot_lit_gene_evidence.py"


rule plot_lit_gene_evidence_soma:
    input:
         gene_metadata = f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather",
         lit_evidence = '../data/external/miriam/lit_gene_dummy_vars.tsv',
         zscore_by_cluster_rep="../output/seurat3-cluster-wf/zscore_by_cluster_rep.feather"
    output: "../output/science_submission/figures/lit-gene-evidence_soma.svg"
    params: cmap=COLORS["heatmap"]
    script: "scripts/plot_lit_gene_evidence_soma.py"


rule plot_deg_chrom_locs:
    input: 
        tpm="../output/seurat3-cluster-wf/tpm_by_cluster_rep.feather",
        deg="../output/seurat3-cluster-wf/germline_deg/GvLPS_{direction}_biased.pkl",
        gene_annot=f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather"
    output: "../output/science_submission/figures/GvLPS_{direction}_biased_chrom_location.svg"
    script: "scripts/plot_locations.py"


rule plot_chrom_bias:
    input:
        tpm="../output/seurat3-cluster-wf/tpm_by_cluster_rep.feather",
        gene_annot=f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather"
    output: "../output/science_submission/figures/{clus}_chrom_enrichment.svg"
    script: "scripts/plot_chrom_bias.py"


rule plot_GvLPS_expression_by_chrom:
    input:
        tpm="../output/seurat3-cluster-wf/tpm_by_cluster_rep.feather",
        biased="../output/seurat3-cluster-wf/germline_deg/GvLPS_{direction}_biased.pkl",
        gene_annot=f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather"
    output: "../output/science_submission/figures/GvLPS_{direction}_expression_by_chrom.svg"
    script: "scripts/plot_GvLPS_expression_by_chrom.py"


rule plot_ptrap_panel:
    input:
        clusters="../output/seurat3-cluster-wf/combined_n3_clusters.feather",
        zscores="../output/seurat3-cluster-wf/zscore_by_cell.feather",
        umap="../output/seurat3-cluster-wf/combined_n3_umap.feather",
        norm="../output/seurat3-cluster-wf/combined_n3_normalized.feather"
    output: "../output/science_submission/figures/ptrap_expression_panel.svg"
    script: "scripts/ptrap_expression_panel.py"

rule plot_literature_panel:
    input:
        gene_annot=f"../references/gene_annotation_dmel_{TAG}.feather",
        clusters="../output/seurat3-cluster-wf/combined_n3_clusters.feather",
        zscores="../output/seurat3-cluster-wf/zscore_by_cell.feather",
        umap="../output/seurat3-cluster-wf/combined_n3_umap.feather",
        norm="../output/seurat3-cluster-wf/combined_n3_normalized.feather"
    output: "../output/science_submission/figures/literature_expression_panel.done"
    params: pattern=lambda wildcards: "../output/science_submission/figures/literature_expression_panel_{cnt}.svg"
    script: "scripts/literature_gene_expression_panel.py"

################################################################################
# Figure 2: Chromosome Expression and Gene Counts
################################################################################
rule data_fig2:
    input: 
        gene_annot=f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather",
        chrom_sizes=f"../lcdb-references/{ASSEMBLY}/{TAG}/fasta/{ASSEMBLY}_{TAG}.chromsizes",
        adult_bulk="../output/expression-atlas-wf/w1118_gene_counts.feather",
        L3_bulk="../output/bulk2-rnaseq-wf/testis_ovary_counts.feather",
        L3_sc_agg="../output/seurat3-cluster-wf/aggegated_gene_counts.feather",
        L3_sc_by_class="../output/seurat3-cluster-wf/aggegated_gene_counts_by_germ_soma.feather",
    output: 
        prop_reads="../output/science_submission/fig2_data_prop_reads.feather",
        pct_expressed="../output/science_submission/fig2_data_pct_expressed.feather"
    script: "scripts/fig2_data.py"


rule plot_fig2:
    input: 
        prop_reads=rules.data_fig2.output.prop_reads,
        pct_expressed=rules.data_fig2.output.pct_expressed
    output: "../output/science_submission/figures/fig2.svg"
    params: colors=COLORS
    script: "scripts/plot_fig2.py"


rule plot_chrom_read_count_panel_scale_scaffold_len:
    input: 
        chrom_sizes=f"../lcdb-references/{ASSEMBLY}/{TAG}/fasta/{ASSEMBLY}_{TAG}.chromsizes",
        adult_bulk="../output/expression-atlas-wf/w1118_gene_counts.feather",
        L3_bulk="../output/bulk2-rnaseq-wf/testis_ovary_counts.feather",
        L3_sc_agg="../output/seurat3-cluster-wf/aggegated_gene_counts.feather",
        L3_sc_by_class="../output/seurat3-cluster-wf/aggegated_gene_counts_by_germ_soma.feather",
    output: "../output/science_submission/figures/chrom_read_count_panel_scale_scaffold_len.svg"
    params: colors=COLORS
    script: "scripts/plot_chrom_read_count_panel_scale_scaffold_len.py"


rule plot_chrom_read_count_panel_scale_num_genes_per_scaffold:
    input: 
        gene_annot=f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather",
        adult_bulk="../output/expression-atlas-wf/w1118_gene_counts.feather",
        L3_bulk="../output/bulk2-rnaseq-wf/testis_ovary_counts.feather",
        L3_sc_agg="../output/seurat3-cluster-wf/aggegated_gene_counts.feather",
        L3_sc_by_class="../output/seurat3-cluster-wf/aggegated_gene_counts_by_germ_soma.feather",
    output: "../output/science_submission/figures/chrom_read_count_panel_scale_num_genes_per_scaffold.svg"
    params: colors=COLORS
    script: "scripts/plot_chrom_read_count_panel_scale_num_genes_per_scaffold.py"


rule plot_chrom_read_count_panel_scale_num_expressed_genes_per_scaffold:
    input: 
        gene_annot=f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather",
        adult_bulk="../output/expression-atlas-wf/w1118_gene_counts.feather",
        L3_bulk="../output/bulk2-rnaseq-wf/testis_ovary_counts.feather",
        L3_sc_agg="../output/seurat3-cluster-wf/aggegated_gene_counts.feather",
        L3_sc_by_class="../output/seurat3-cluster-wf/aggegated_gene_counts_by_germ_soma.feather",
    output: "../output/science_submission/figures/chrom_read_count_panel_scale_num_expressed_genes_per_scaffold.svg"
    params: colors=COLORS
    script: "scripts/plot_chrom_read_count_panel_scale_num_expressed_genes_per_scaffold.py"


rule plot_chrom_prop_expressed_all_genes:
    input: 
        gene_annot=f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather",
        adult_bulk="../output/expression-atlas-wf/w1118_gene_counts.feather",
        L3_bulk="../output/bulk2-rnaseq-wf/testis_ovary_counts.feather",
        L3_sc_agg="../output/seurat3-cluster-wf/aggegated_gene_counts.feather",
        L3_sc_by_class="../output/seurat3-cluster-wf/aggegated_gene_counts_by_germ_soma.feather",
    output: "../output/science_submission/figures/chrom_prop_expressed_all_genes.svg"
    params: colors=COLORS
    script: "scripts/plot_chrom_prop_expressed_all_genes.py"


rule plot_chrom_prop_expressed_genes_in_experiment:
    input: 
        gene_annot=f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather",
        adult_bulk="../output/expression-atlas-wf/w1118_gene_counts.feather",
        L3_bulk="../output/bulk2-rnaseq-wf/testis_ovary_counts.feather",
        L3_sc_agg="../output/seurat3-cluster-wf/aggegated_gene_counts.feather",
        L3_sc_by_class="../output/seurat3-cluster-wf/aggegated_gene_counts_by_germ_soma.feather",
    output: "../output/science_submission/figures/chrom_prop_expressed_genes_in_experiment.svg"
    params: colors=COLORS
    script: "scripts/plot_chrom_prop_expressed_genes_in_experiment.py"


################################################################################
# Figure 3: X + 4 Inactivation
################################################################################
rule plot_fig3:
    input: 
        gene_annot=f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather",
        all='../output/x-to-a-wf/db/expressed.dat',
        common='../output/x-to-a-wf/db/commonly_expressed.dat',
        tpm='../output/seurat3-cluster-wf/tpm_by_cluster.feather',
        common_fbgns="../output/cellselection-wf/commonly_expressed_genes.pkl",
    params:
          cluster_color=COLORS["clusters"],
          cluster_order=config['cluster_order'],
    output: "../output/science_submission/figures/fig3.svg"
    script: "scripts/fig3.py"


################################################################################
# Tables
################################################################################
rule cell_metadata:
    input: 
        metadata="../output/seurat3-cluster-wf/combined_n3_metadata.feather",
        clusters="../output/seurat3-cluster-wf/combined_n3_clusters.feather",
        cell_calls=expand("../output/cellselection-wf/{sample}_combined_cell_calls.feather", sample=["testis1", "testis2", "testis3"]),
        scrublets=expand("../output/cellselection-wf/{sample}_scrublet_dublets.txt", sample=["testis1", "testis2", "testis3"]),
        umap="../output/seurat3-cluster-wf/combined_n3_umap.feather",
    output: "../output/science_submission/cell_metadata.tsv"
    script: "scripts/cell_metadata_table.py"


rule gene_metadata:
    input: 
        gene_annot="../references/gene_annotation_dmel_r6-26.feather",
        biomarkers="../output/seurat3-cluster-wf/combined_n3_biomarkers.feather",
        raw="../output/seurat3-cluster-wf/raw_by_cluster_rep.feather",
        tpm="../output/seurat3-cluster-wf/tpm_by_cluster_rep.feather",
        zscore="../output/seurat3-cluster-wf/zscore_by_cluster_rep.feather",
    params: 
        cluster_annot=config['cluster_annot'],
        cluster_order=config['cluster_order'],
    output: "../output/science_submission/gene_metadata.tsv"
    script: "scripts/gene_metadata_table.py"


rule raw_to_tsv:
    input: '../output/cellselection-wf/raw.feather'
    output: '../output/science_submission/scRNAseq_raw_counts.tsv.gz'
    run:
        pd.read_feather(input[0]).to_csv(output[0], index=False, sep="\t")


rule seurat_normalized_to_tsv:
    input: '../output/seurat3-cluster-wf/combined_n3_normalized.feather'
    output: '../output/science_submission/scRNAseq_seurat_normalized_counts.tsv.gz'
    run:
        pd.read_feather(input[0]).drop("gene_symbol", axis=1).to_csv(output[0], index=False, sep="\t")


rule raw_by_celltype_to_tsv:
    input: '../output/seurat3-cluster-wf/raw_by_cluster_rep.feather'
    output: '../output/science_submission/scRNAseq_raw_counts_by_celltype_rep.tsv.gz'
    run:
        pd.pivot_table(
            pd.read_feather(input[0]),
            index="FBgn",
            columns=["cluster", "rep"],
            values="UMI",
            aggfunc="first"
        ).to_csv(output[0], sep="\t")


rule tpm_by_celltype_to_tsv:
    input: '../output/seurat3-cluster-wf/tpm_by_cluster_rep.feather'
    output: '../output/science_submission/scRNAseq_tpm_counts_by_celltype_rep.tsv.gz'
    run:
        pd.pivot_table(
            pd.read_feather(input[0]),
            index="FBgn",
            columns=["cluster", "rep"],
            values="TPM",
            aggfunc="first"
        ).to_csv(output[0], sep="\t")


rule zscores_by_celltype_to_tsv:
    input: '../output/seurat3-cluster-wf/zscore_by_cluster_rep.feather'
    output: '../output/science_submission/scRNAseq_zscores_by_celltype_rep.tsv.gz'
    run:
        pd.pivot_table(
            pd.read_feather(input[0]),
            index="FBgn",
            columns=["cluster", "rep"],
            values="zscore",
            aggfunc="first"
        ).to_csv(output[0], sep="\t")

rule biomarkers_to_tsv:
    input: "../output/seurat3-cluster-wf/combined_n3_biomarkers.feather"
    output: "../output/science_submission/biomarkers.tsv"
    params: cluster_annot = config["cluster_annot"]
    run:
        (
            pd.read_feather(input[0])
            .assign(cluster=lambda x: x.cluster.map(lambda y: params.cluster_annot[y]))
            .to_csv(output[0], index=False, sep="\t")
        )
    