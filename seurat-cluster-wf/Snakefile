from pathlib import Path
import re

import pandas as pd

from larval_gonad.config import read_config

common_config = read_config('../config/common.yaml')
assembly = common_config['assembly']
tag = common_config['tag']

configfile: 'config/config.yaml'

sample_table = pd.read_csv('../config/scrnaseq-sampletable.tsv', sep='\t', index_col=0)
SAMPLES = sample_table.index.values.tolist()

localrules: fbgn2chrom, unzip_cellranger3

rule targets:
    input: 
        expand(
            '../output/seurat-cluster-wf/{cellranger}/{cutoffs}/{sample}/seurat_individual.html', 
            cellranger=['cellranger-force-wf', 'cellranger3-wf'],
            cutoffs=[
                'gl-200_gh-None_ul-None_uh-None',
                'gl-200_gh-None_ul-None_uh-1e5',
            ],
            sample=SAMPLES
        ),
        expand(
            '../output/seurat-cluster-wf/{cellranger}/{cutoffs}/combined/seurat_combined.html', 
            cellranger=['cellranger-force-wf', 'cellranger3-wf'],
            cutoffs=[
                'gl-200_gh-None_ul-None_uh-None',
                'gl-200_gh-None_ul-None_uh-1e5',
            ]
        ),


rule fbgn2chrom:
    output: 
        '../output/fbgn2chrom.tsv'
    script: 
        '../bin/fbgn2chrom.py'


rule unzip_cellranger3:
    input: 
        barcodes = config['data_pattern']['cellranger3-wf'].replace('matrix.mtx', 'barcodes.tsv') + '.gz',
        features = config['data_pattern']['cellranger3-wf'].replace('matrix.mtx', 'features.tsv') + '.gz',
        matrix = config['data_pattern']['cellranger3-wf'] + '.gz'
    output: 
        barcodes = config['data_pattern']['cellranger3-wf'].replace('matrix.mtx', 'barcodes.tsv'),
        features = config['data_pattern']['cellranger3-wf'].replace('matrix.mtx', 'genes.tsv'),
        matrix = config['data_pattern']['cellranger3-wf']
    shell:"""
        gunzip -c {input.barcodes} > {output.barcodes} && \
        gunzip -c {input.features} > {output.features} && \
        gunzip -c {input.matrix} > {output.matrix}
    """


def _seurat_individual_sample(wildcards):
    return expand(
        config['data_pattern'][wildcards.cellranger], assembly=assembly, tag=tag, sample=wildcards.sample
    )


rule seurat_individual_sample:
    input:
        rmd = 'scripts/cluster_individual_sample.Rmd',
        fbgn2chrom = rules.fbgn2chrom.output[0],
        data = _seurat_individual_sample
    output: 
        '../output/seurat-cluster-wf/{cellranger}/{cutoffs}/{sample}/seurat_individual.html', 
    params:
        rep = lambda wildcards: 'rep' + wildcards.sample.replace('testis', ''),
        gene_low = lambda wildcards: re.findall('gl-(.*?)_.*', wildcards.cutoffs)[0],
        gene_high = lambda wildcards: re.findall('.*gh-(.*?)_.*', wildcards.cutoffs)[0],
        umi_low = lambda wildcards: re.findall('.*ul-(.*?)_.*', wildcards.cutoffs)[0],
        umi_high = lambda wildcards: re.findall('.*uh-(.*)', wildcards.cutoffs)[0]
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 16,
        time_hr=lambda wildcards, attempt: attempt * 8
    script: 'scripts/cluster_individual_sample.Rmd'


rule dummy_seurat_individual_sample:
    input: rules.seurat_individual_sample.output
    output: 
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/seurat.Robj',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/metadata.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/raw.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/normalized_read_counts.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/dispersion.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/var_genes.txt',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/scaled.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/principal_components_cell.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/principal_components_gene.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/principal_components_stdev.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/cca_cell.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/cca_gene.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/cca_gene_full.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/cca_aligned_cell.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/clusters.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/tsne.tsv'
    run:
        for fname in output:
            Path(fname).touch()


def _combine_and_cluster(wildcards):
    return expand(
        config['data_pattern'][wildcards.cellranger], assembly=assembly, tag=tag, sample=SAMPLES[:-1]
    )


rule combine_and_cluster:
    input:
        rmd = 'scripts/combine_and_cluster.Rmd',
        fbgn2chrom = rules.fbgn2chrom.output[0],
        data = _combine_and_cluster
    output: 
        '../output/seurat-cluster-wf/{cellranger}/{cutoffs}/combined/seurat_combined.html', 
    params:
        samples = SAMPLES[:-1],
        gene_low = lambda wildcards: re.findall('gl-(.*?)_.*', wildcards.cutoffs)[0],
        gene_high = lambda wildcards: re.findall('.*gh-(.*?)_.*', wildcards.cutoffs)[0],
        umi_low = lambda wildcards: re.findall('.*ul-(.*?)_.*', wildcards.cutoffs)[0],
        umi_high = lambda wildcards: re.findall('.*uh-(.*)', wildcards.cutoffs)[0]
    resources:
        mem_gb=lambda wildcards, attempt: attempt * 16,
        time_hr=lambda wildcards, attempt: attempt * 8
    script: 'scripts/combine_and_cluster.Rmd'


rule dummy_combined_and_cluster:
    input: rules.combine_and_cluster.output
    output: 
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/seurat.Robj',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/metadata.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/raw.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/normalized_read_counts.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/dispersion.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/var_genes.txt',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/scaled.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/principal_components_cell.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/principal_components_gene.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/principal_components_stdev.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/cca_cell.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/cca_gene.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/cca_gene_full.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/cca_aligned_cell.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/clusters.tsv',
        '../output/seurat-cluster-wf/{cutoffs}/{sample}/tsne.tsv'
    run:
        for fname in output:
            Path(fname).touch()
