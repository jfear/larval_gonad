"""Testis Single Cell Workflow."""
import sys
import os
from textwrap import dedent
import yaml
import tempfile
import pandas as pd
from lcdblib.snakemake import helpers, aligners
from lcdblib.utils import utils
from larval_gonad.normalization import rpkm, cpm, tpm
from larval_gonad.config import config as sconfig
from larval_gonad.scRNAseq import Seurat

sys.path.insert(0, srcdir('../lcdb-wf'))
from lib import common
from lib.patterns_targets import RNASeqConfig


configfile: 'config/config.yaml'
shell.prefix('set -euo pipefail; export TMPDIR={};'.format(common.tempdir_for_biowulf()))
shell.executable('/bin/bash')

c = RNASeqConfig(config, 'config/scrnaseq_patterns.yaml')


# ----------------------------------------------------------------------------
# RULES
# ----------------------------------------------------------------------------
final_targets = utils.flatten((
    utils.flatten(c.targets['cellranger']['bam']),
    utils.flatten(c.targets['cellranger_force']['bam']),
    'scripts/scrnaseq_combine_force.html',
    'scripts/germcell_differential_expression.html',
    'data/seurat_norm_by_cluster.parquet',
    'data/raw.parquet',
    'data/raw_by_cluster.parquet',
    'data/cpm.parquet',
    'data/rpkm.parquet',
    'data/tpm.parquet',
    'data/tpm_zscore.parquet',
    'data/rpkm_zscore.parquet',
))

rule targets:
    """
    Final targets to create
    """
    input: final_targets


rule fix_gtf:
    input:
        gtf = [c.refdict[c.assembly][config['gtf']['tag']]['gtf']]
    output: '../references/dmel-all-r6.16.modFix.gtf'
    shell: """
        awk 'BEGIN{{FS="\t"}}{{
            if (!($9 ~ /FBgn0002781/ && ($7 == "+" || $7 == "."))){{print $0}}
        }}' {input} > {output[0]}
    """


rule cellranger_mkref:
    input:
        gtf = rules.fix_gtf.output,
        fasta = [c.refdict[c.assembly][config['gtf']['tag']]['fasta']]
    output:
        fasta = '../references/dm6.16/fasta/genome.fa',
        gtf = '../references/dm6.16/genes/genes.gtf',
        star = '../references/dm6.16/star/SA'
    threads: 6
    resources:
        mem_gb = lambda wildcards, attempt: attempt * 8,
        time_hr = lambda wildcards, attempt: attempt * 4
    shell:
        'module load cellranger/2.1.1 && '
        'cd ../references/ && '
        'cellranger mkref '
        '--genome=dm6.16 '
        '--fasta={input.fasta} '
        '--genes={input.gtf} '
        '--nthreads={threads} '
        '--memgb={resources.mem_gb} '


rule cellranger:
    input:
        ref = rules.cellranger_mkref.output.star,
    output: '{sample_dir}/{sample,testis1|testis2|testis3}/outs/possorted_genome_bam.bam'
    threads: 16
    resources:
        mem_gb = lambda wildcards, attempt: attempt * 25,
        time_hr = lambda wildcards, attempt: attempt * 24
    run:
        stable = c.sampletable.set_index('samplename')
        record = stable.loc[wildcards.sample, :]
        sample = record['sample']
        fname = record['orig_filename']
        cmd = (
            f'module load cellranger/2.1.1 && '
            f'cd {c.sample_dir} && '
            f'rm -rf {wildcards.sample} &&'
            f'cellranger count '
            f'--id={wildcards.sample} '
            f'--transcriptome=../../../references/dm6.16 '
            f'--fastqs={fname} '
            f'--sample={sample} '
            f'--localcores={threads} '
            f'--localmem={resources.mem_gb} '
        )
        shell(cmd)


rule cellranger_force:
    input:
        ref = rules.cellranger_mkref.output.star,
    output:
        bam='{sample_dir}/{sample,testis1|testis2|testis3}_force/outs/possorted_genome_bam.bam',
        h5='{sample_dir}/{sample,testis1|testis2|testis3}_force/outs/filtered_gene_bc_matrices_h5.h5'
    threads: 16
    resources:
        mem_gb = lambda wildcards, attempt: attempt * 25,
        time_hr = lambda wildcards, attempt: attempt * 24
    run:
        stable = c.sampletable.set_index('samplename')
        record = stable.loc[wildcards.sample, :]
        sample = record['sample']
        fname = record['orig_filename']
        cmd = (
            f'module load cellranger/2.1.1 && '
            f'cd {c.sample_dir} && '
            f'rm -rf {wildcards.sample}_force &&'
            f'cellranger count '
            f'--id={wildcards.sample}_force '
            f'--transcriptome=../../../references/dm6.16 '
            f'--fastqs={fname} '
            f'--sample={sample} '
            f'--localcores={threads} '
            f'--localmem={resources.mem_gb} '
            f'--force-cells=3000 '
        )
        shell(cmd)


rule scrnaseq_rmarkdown:
    """
    Run and render the RMarkdown file that performs differential expression
    """
    input:
        mtx1=utils.flatten(c.targets['cellranger_force']['filtered_h5']),
        rmd='scripts/scrnaseq_combine_force.Rmd',
    output:
        html='scripts/scrnaseq_combine_force.html',
        raw='data/scrnaseq_combine_force/raw.tsv',
        norm='data/scrnaseq_combine_force/normalized_read_counts.tsv',
        cluster='data/scrnaseq_combine_force/clusters.tsv',
        tsne='data/scrnaseq_combine_force/tsne.tsv',
        robj='data/scrnaseq_combine_force/seurat.Robj',
    shell:
        'Rscript -e '
        '''"rmarkdown::render('{input.rmd}', 'knitrBootstrap::bootstrap_document')"'''


rule germcell_rmarkdown:
    """
    Run and render the RMarkdown file that performs differential expression
    """
    input:
        robj=rules.scrnaseq_rmarkdown.output.robj,
        rmd='scripts/germcell_differential_expression.Rmd',
    output:
        'scripts/germcell_differential_expression.html',
        'data/gonia_vs_cytes.tsv',
        'data/gonia_vs_cytes_plus_11.tsv',
        'data/gonia_vs_early.tsv',
        'data/gonia_vs_mid.tsv',
        'data/gonia_vs_late.tsv',
        'data/gonia_vs_eleven.tsv',
        'data/early_vs_mid.tsv',
        'data/early_vs_late.tsv',
        'data/early_vs_eleven.tsv',
        'data/mid_vs_late.tsv',
        'data/mid_vs_eleven.tsv',
        'data/late_vs_eleven.tsv',
    shell:
        'Rscript -e '
        '''"rmarkdown::render('{input.rmd}', 'knitrBootstrap::bootstrap_document')"'''


rule raw:
    input: 'data/scrnaseq_combine_force/raw.tsv',
    output: 'data/raw.parquet'
    run:
        import pandas as pd
        raw = pd.read_csv(input[0], sep='\t', index_col=0)
        raw.index.name = 'FBgn'
        raw.to_parquet(output[0])


rule agg_raw_counts:
    input:
        cnts = 'data/scrnaseq_combine_force/raw.tsv',
        clusters = 'data/scrnaseq_combine_force/clusters.tsv',
    output: 'data/raw_by_cluster.parquet'
    params:
        seurat_dir = 'data/scrnaseq_combine_force',
        resolution = sconfig['resolution']
    run:
        s = Seurat(params.seurat_dir)
        clusters = s.get_clusters(resolution=params.resolution)
        raw = s.get_raw().T.join(clusters).groupby('cluster').sum().T
        raw.columns = [sconfig['cluster_annot'][x] for x in raw.columns]
        raw.index.name = 'FBgn'
        raw[sconfig['cluster_order']].to_parquet(output[0])


rule agg_norm_counts:
    input:
        cnts = 'data/scrnaseq_combine_force/normalized_read_counts.tsv',
        clusters = 'data/scrnaseq_combine_force/clusters.tsv',
    output: 'data/seurat_norm_by_cluster.parquet'
    params:
        seurat_dir = 'data/scrnaseq_combine_force',
        resolution = sconfig['resolution']
    run:
        s = Seurat(params.seurat_dir)
        clusters = s.get_clusters(resolution=params.resolution)
        norm = s.get_normalized_read_counts().T.join(clusters).groupby('cluster').sum().T
        norm.columns = [sconfig['cluster_annot'][x] for x in norm.columns]
        norm.index.name = 'FBgn'
        norm[sconfig['cluster_order']].to_parquet(output[0])


rule cpm:
    input:
        cnts = 'data/raw_by_cluster.parquet',
        lens = '../output/gene_ts_lengths.tsv'
    output: 'data/cpm.parquet',
    run:
        from larval_gonad.normalization import cpm
        raw = pd.read_parquet(input.cnts)
        res = cpm(raw)
        res.to_parquet(output[0])


rule rpkm:
    input:
        cnts = 'data/raw_by_cluster.parquet',
        lens = '../output/gene_ts_lengths.tsv'
    output: 'data/rpkm.parquet',
    run:
        from larval_gonad.normalization import rpkm
        raw = pd.read_parquet(input.cnts)
        lens = pd.read_csv(input.lens, sep='\t').set_index('FBgn').gene_ts_length
        lens = lens[raw.index.tolist()]

        res = rpkm(raw, lens)
        res.to_parquet(output[0])


rule tpm:
    input:
        cnts = 'data/raw_by_cluster.parquet',
        lens = '../output/gene_ts_lengths.tsv'
    output: 'data/tpm.parquet',
    run:
        from larval_gonad.normalization import tpm
        raw = pd.read_parquet(input.cnts)
        lens = pd.read_csv(input.lens, sep='\t').set_index('FBgn').gene_ts_length
        lens = lens[raw.index.tolist()]

        res = tpm(raw, lens)
        res.to_parquet(output[0])


rule tpm_zscore:
    input:
        cnts = 'data/tpm.parquet',
    output: 'data/tpm_zscore.parquet',
    run:
        from larval_gonad.normalization import zscore
        tpm = pd.read_parquet(input.cnts)
        res = zscore(tpm)
        res.to_parquet(output[0])


rule rpkm_zscore:
    input:
        cnts = 'data/rpkm.parquet',
    output: 'data/rpkm_zscore.parquet',
    run:
        from larval_gonad.normalization import zscore
        rpkm = pd.read_parquet(input.cnts)
        res = zscore(rpkm)
        res.to_parquet(output[0])
