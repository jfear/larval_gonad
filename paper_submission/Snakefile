"""Scrip to generate figures and needed material for the Science Submssion"""
import os
import yaml
from pathlib import Path

import pandas as pd

from larval_gonad.config import read_config
from larval_gonad.io import safe_gene_name

workdir: "."
# configfile: "../config/common.yaml"
config = read_config("../config/common.yaml")
colors_config = read_config("../config/colors.yaml")

SAMPLES = ["testis1", "testis2", "testis3"]

REFERENCES_DIR = os.environ.get("REFERENCES_DIR", config.get("REFERENCES_DIR", ""))
ASSEMBLY = config["assembly"]
TAG = config["tag"]

LIT_GENES = read_config("../config/literature_genes.yaml")
PTRAP_GENES = read_config("../config/ptrap_genes.yaml")
COLORS = yaml.full_load(open("../config/colors.yaml"))

FBGN2SYMBOL = (
    pd.read_feather("../references/gene_annotation_dmel_r6-26.feather", columns=["FBgn", "gene_symbol"])
    .set_index("FBgn")
    .squeeze()
    .reindex(pd.read_feather("../output/seurat3-cluster-wf/combined_n3_normalized.feather", columns=["FBgn"]).FBgn.unique().tolist())
    .map(safe_gene_name)
)

FBGN2CHROM = (
    pd.read_feather("../references/gene_annotation_dmel_r6-26.feather", columns=["FBgn", "gene_symbol", "FB_chrom"])
    .set_index(["FBgn", "gene_symbol"])
    .squeeze()
    .rename("Chromosome")
    .reset_index()
)

wildcard_constraints: 
    sample="testis\d"

rule all:
    input: 
        # Main Figures
        "../output/paper_submission/figures/fig1.done",
        "../output/paper_submission/figures/fig2.done",
        "../output/paper_submission/figures/fig3.done",
        "../output/paper_submission/figures/fig4.done",
        "../output/paper_submission/figures/fig5.done",
        "../output/paper_submission/figures/fig6.done",

        # Supplemental Figures

        # Supplemental Files
        "../output/paper_submission/TableS1.xlsx", # Table S1
        "../output/paper_submission/TableS2.xlsx", # Table S2
        '../output/paper_submission/scRNAseq_raw_counts.tsv.gz',
        '../output/paper_submission/scRNAseq_seurat_normalized_counts.tsv.gz',

################################################################################
# Figure 1 (Alternative): Chromosome Expression By Tissue
################################################################################
rule chrom_expression_by_tissue_data:
    input: 
        gene_annot=f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather",
        w1118="../output/expression-atlas-wf/w1118_gene_counts.feather",
        OreR="../output/expression-atlas-wf/OreR_gene_counts.feather",
        L3_bulk="../output/bulk-rnaseq-wf/testis_ovary_counts.feather",
    output: "../output/paper_submission/chrom_expression_by_tissue_data_avg_tpm_per_chrom.feather",
    script: "scripts/chrom_expression_by_tissue_data.py"


rule chrom_expression_by_tissue_stats:
    input: rules.chrom_expression_by_tissue_data.output[0]
    output: "../output/paper_submission/chrom_expression_by_tissue_stats.tsv"
    script: "scripts/chrom_expression_by_tissue_stats.py"


rule chrom_expression_by_tissue:
    input: 
        rules.chrom_expression_by_tissue_data.output[0],
        rules.chrom_expression_by_tissue_stats.output[0]
    output: "../output/paper_submission/figures/chrom_expression_by_tissue.svg"
    params: colors=COLORS
    script: "scripts/chrom_expression_by_tissue.py"

################################################################################
# Figure 1 (Current): Sex Biased Expression By Chromosome
################################################################################
rule sex_biased_expression_by_tissue_data:
    input: 
        adult="../output/expression-atlas-wf/dmel_sex_biased_expression.feather",
        larval="../output/bulk-rnaseq-wf/deg/bulk_testis_vs_ovary.tsv",
        gene_annot=f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather",
    output: "../output/paper_submission/sex_biased_expression_by_tissue.feather",
    script: "scripts/sex_biased_expression_by_tissue_data.py"


rule stats_sex_biased_expression_by_tissue:
    """Runs chi-square tests for each tissue by chromosome element."""
    input: rules.sex_biased_expression_by_tissue_data.output[0]
    output: 
        full_stats="../output/paper_submission/stats_full_sex_biased_expression_by_tissue.tsv",
        filtered_stats="../output/paper_submission/stats_filtered_sex_biased_expression_by_tissue.tsv",
    params: chrom_order=config["chrom_order"]
    script: "scripts/stats_sex_biased_expression_by_tissue.py"


rule plot_sex_biased_expression_by_tissue:
    input: 
        data=rules.sex_biased_expression_by_tissue_data.output[0],
        stats=rules.stats_sex_biased_expression_by_tissue.output.filtered_stats,
    output: "../output/paper_submission/figures/sex_biased_expression_by_tissue.svg"
    params: chrom_order=config["chrom_order"]
    script: "scripts/plot_sex_biased_expression_by_tissue.py"


rule fig1:
    """Figure 1 parts"""
    input: rules.plot_sex_biased_expression_by_tissue.output[0]
    output: touch("../output/paper_submission/figures/fig1.done")


################################################################################
# Figure 2: scRNA-Seq intro, cluster, and annotate
################################################################################
rule plot_cluster_legend:
    params:
        cluster_colors=COLORS["clusters"],
        legend_names=config["legend_names"]
    output: "../output/paper_submission/figures/cluster-legend.svg"
    script: "scripts/plot_cluster_legend.py"


rule plot_gene_umap:
    input:
        gene_annot=f"../references/gene_annotation_dmel_{TAG}.feather",
        zscores="../output/seurat3-cluster-wf/zscore_by_cell.feather",
        umap="../output/seurat3-cluster-wf/combined_n3_umap.feather",
    output: "../output/paper_submission/figures/fig2_umap.svg"
    script: "scripts/plot_gene_umap.py"


rule plot_gene_expression_pattern:
    input:
        gene_annot=f"../references/gene_annotation_dmel_{TAG}.feather",
        clusters="../output/seurat3-cluster-wf/combined_n3_clusters.feather",
        raw="../output/cellselection-wf/raw.feather",
    output: 
        tpm="../output/paper_submission/figures/fig2_tpm.svg",
        scaled="../output/paper_submission/figures/fig2_scaled_tpm.svg",
    params: 
        colors=colors_config["clusters"],
        cluster_order=config["cluster_order"]
    script: "scripts/plot_gene_expression_pattern.py"


rule fig2:
    """Figure 2 parts"""
    input: 
        rules.plot_cluster_legend.output[0],
        rules.plot_gene_umap.output[0],
        rules.plot_gene_expression_pattern.output[0],
        "../output/seurat3-cluster-wf/combined_n3_figures/uniq_and_multi_biomarkers.svg",
    output: touch("../output/paper_submission/figures/fig2.done")

################################################################################
# Figure 3: Cell Type Level Expression
################################################################################
rule plot_bulk_vs_sum_scrnaseq:
    input:
        bulk="../output/bulk-rnaseq-wf/rnaseq_aggregation/gene_level_counts.tsv",
        sc="../output/cellselection-wf/raw.feather",
        gene_annot="../references/gene_annotation_dmel_r6-26.feather",
        male_biased="../output/bulk-rnaseq-wf/deg/larval_testis_biased_fbgns.pkl",
    output: "../output/paper_submission/figures/bulk_vs_sum_scrnaseq.svg"
    script: "scripts/plot_bulk_vs_sum_scrnaseq.py"


rule gsea_by_cell_type:
    input:
        sc="../output/cellselection-wf/raw.feather",
        clusters="../output/seurat3-cluster-wf/combined_n3_clusters.feather",
        expressed="../output/cellselection-wf/expressed_genes.pkl",
        male_biased="../output/bulk-rnaseq-wf/deg/larval_testis_biased_fbgns.pkl",
        gene_annot="../references/gene_annotation_dmel_r6-26.feather",
    output: "../output/paper_submission/gsea_by_cell_type.feather",
    threads: 20
    script: "scripts/gsea_by_cell_type.py"


rule stats_gsea_pairwise_comparisons_of_cell_types:
    """Permutation test for all pairwise cell-type comparisons."""
    input: rules.gsea_by_cell_type.output[0]
    output: "../output/paper_submission/stats_gsea_pairwise_comparisons_of_cell_types.tsv"
    threads: 20
    params:
        order=config['cluster_order'],
    script: "scripts/stats_gsea_pairwise_comparisons_of_cell_types.py"


rule plot_gsea_by_cell_type:
    """Boxplots of GSEA enrichment scores by cell-type."""
    input: 
        rules.gsea_by_cell_type.output[0], 
        rules.stats_gsea_pairwise_comparisons_of_cell_types.output[0]
    output: "../output/paper_submission/figures/gsea_by_cell_type.svg",
    params:
        colors=colors_config["clusters"],
        order=config["cluster_order"],
        names=config["cluster_names"],
    script: "scripts/plot_gsea_by_cell_type.py"


# Compare total expression with different gene sets
rule plot_tau_and_tsps_distributions_by_cell_type:
    input:
        expressed_fbgns="../output/cellselection-wf/expressed_genes.pkl",
        widely_expressed_fbgns="../output/cellselection-wf/commonly_expressed_genes.pkl",
        tau="../output/expression-atlas-wf/dmel_tau.feather",
        tsps="../output/expression-atlas-wf/dmel_tsps.feather",
        clusters="../output/seurat3-cluster-wf/raw_by_cluster.feather",
    params:
        colors=COLORS["clusters"],
        order=config['cluster_order'],
        names=config["cluster_names"],
    output: "../output/paper_submission/figures/tau_tsps_distributions_by_cell_type.svg"
    script: "scripts/plot_tau_tsps_distributions_by_cell_type.py"


rule stats_gene_sets_total_expression_by_cell_type:
    """Pairwise comparisons of TPM normalized counts by cell-type."""
    input:
        tpm="../output/seurat3-cluster-wf/tpm_by_cluster.feather",
        expressed_fbgns="../output/cellselection-wf/expressed_genes.pkl",
        widely_expressed_fbgns="../output/cellselection-wf/commonly_expressed_genes.pkl",
        tau_fbgns="../output/expression-atlas-wf/dmel_male_tau_fbgns.pkl",
        tsps_fbgns="../output/expression-atlas-wf/dmel_male_tsps_fbgns.pkl",
    output: "../output/paper_submission/stats_gene_sets_total_expression_by_cell_type.tsv"
    params:
        order=config['cluster_order'],
    script: "scripts/stats_gene_sets_total_expression_by_cell_type.py"


rule plot_total_expression_by_cell_type:
    """Boxplots of TPM normalized counts by cell-type."""
    input:
        gene_annot="../references/gene_annotation_dmel_r6-26.feather",
        tpm="../output/seurat3-cluster-wf/tpm_by_cluster.feather",
        expressed_fbgns="../output/cellselection-wf/expressed_genes.pkl",
        widely_expressed_fbgns="../output/cellselection-wf/commonly_expressed_genes.pkl",
        tau_fbgns="../output/expression-atlas-wf/dmel_male_tau_fbgns.pkl",
        tsps_fbgns="../output/expression-atlas-wf/dmel_male_tsps_fbgns.pkl",
        _=rules.stats_gene_sets_total_expression_by_cell_type.output[0]
    output: "../output/paper_submission/figures/total_expression_by_cell_type.svg"
    params:
        colors=COLORS["clusters"],
        order=config['cluster_order'],
        names=config["cluster_names"],
    script: "scripts/plot_total_expression_by_cell_type.py"


rule plot_gene_ontology_all_genes:
    """GO ribbon for all expressed genes."""
    input:
        background="../references/gene_annotation_dmel_r6-26.tsv",
        target_genes="../output/cellselection-wf/expressed_genes.pkl",
    output: 
        summary="../output/paper_submission/stats_gene_ontology_all_genes.tsv",
        fig="../output/paper_submission/figures/gene_ontology_all_genes.svg",
    script: "scripts/plot_gene_ontology.py"


rule plot_gene_ontology_widely_expressed_genes:
    """GO ribbon for widely expressed genes."""
    input:
        background="../output/cellselection-wf/expressed_genes.pkl",
        target_genes="../output/cellselection-wf/commonly_expressed_genes.pkl",
    output: 
        summary="../output/paper_submission/stats_gene_ontology_widely_expressed_genes.tsv",
        fig="../output/paper_submission/figures/gene_ontology_widely_expressed_genes.svg",
    script: "scripts/plot_gene_ontology.py"


# Compare chromosome level expression with different gene sets
rule stats_chromosome_proportions_by_cell_type:
    """Pairwise comparions of the proportion of reads mapping to chromosome element by cluster."""
    input: 
        clusters="../output/seurat3-cluster-wf/combined_n3_clusters.feather",
        all_genes="../output/chromosome-expression-wf/cell_level_chromosome_proportions_all_expressed_genes.feather",
        tau_genes="../output/chromosome-expression-wf/cell_level_chromosome_proportions_tau.feather",
        tsps_genes="../output/chromosome-expression-wf/cell_level_chromosome_proportions_tsps.feather",
        widely_expressed_genes="../output/chromosome-expression-wf/cell_level_chromosome_proportions_widely_expressed_genes.feather",
    output: "../output/paper_submission/stats_chromosome_proportions_by_cell_type.tsv"
    threads: 20
    params: 
        chrom_order=config["chrom_order"],
        cluster_order=config["cluster_order"],
    script: "scripts/stats_chromosome_proportions_by_cell_type.py"


rule plot_chromosome_proportions_by_cell_type:
    """Box plots showing the proportion of reads mapping to chromosome element by cluster."""
    input: 
        clusters="../output/seurat3-cluster-wf/combined_n3_clusters.feather",
        all_genes="../output/chromosome-expression-wf/cell_level_chromosome_proportions_all_expressed_genes.feather",
        tau_genes="../output/chromosome-expression-wf/cell_level_chromosome_proportions_tau.feather",
        tsps_genes="../output/chromosome-expression-wf/cell_level_chromosome_proportions_tsps.feather",
        widely_expressed_genes="../output/chromosome-expression-wf/cell_level_chromosome_proportions_widely_expressed_genes.feather",
    output: "../output/paper_submission/figures/chromosome_proportions_by_cell_type.svg"
    params: 
        chrom_order=config["chrom_order"],
        cluster_order=config["cluster_order"],
        cluster_names=config["cluster_names"],
        cluster_colors=colors_config["clusters"],
    script: "scripts/plot_chromosome_proportions_by_cell_type.py"


    input: 
        gene_annot="../references/gene_annotation_dmel_r6-26.feather",
        expressed_fbgns="../output/cellselection-wf/expressed_genes.pkl",
        widely_expressed_fbgns="../output/cellselection-wf/commonly_expressed_genes.pkl",
        tpm="../output/seurat3-cluster-wf/tpm_by_cluster.feather",
    params:
          cluster_color=COLORS["clusters"],
          cluster_order=config['cluster_order'],
    output: "../output/paper_submission/figures/total_expression_by_cell_type_boxplots.svg"
    script: "scripts/plot_total_expression_by_cell_type.py"

#
rule fig3:
    """Figure 3 parts"""
    input: 
        rules.plot_bulk_vs_sum_scrnaseq.output[0],
        rules.plot_gsea_by_cell_type.output[0],
        rules.plot_total_expression_by_cell_type.output[0],
        rules.plot_gene_ontology_all_genes.output[0],
        rules.plot_gene_ontology_widely_expressed_genes.output[0],
        rules.plot_chromosome_proportions_by_cell_type.output[0],
    output: touch("../output/paper_submission/figures/fig3.done")


################################################################################
# Figure 4: X, Y, 4 to Autosome Ratios
###############################################################################
rule plot_autosome_ratios:
    input: 
        gene_annot="../references/gene_annotation_dmel_r6-26.feather",
        expressed_ratios="../output/x-to-a-wf/db/expressed.dat",
        widely_expressed_ratios="../output/x-to-a-wf/db/commonly_expressed.dat",
    params:
          cluster_color=COLORS["clusters"],
          cluster_order=config['cluster_order'],
    output: "../output/paper_submission/figures/autosome_ratios.svg"
    script: "scripts/plot_autosome_ratios.py"


rule fig4:
    """Figure 4 parts"""
    input:
        rules.plot_autosome_ratios.output[0]
    output: touch("../output/paper_submission/figures/fig4.done")


################################################################################
# Figure 5: Chromosome Spatial Localization
################################################################################
rule chromosome_territory:
    input:
        volume="../output/fish-wf/chrom_volume.svg",
        sphere="../output/fish-wf/sphericity.svg"
    output: "../output/paper_submission/figures/chromosome_territory.svg"
    script: "scripts/chromosome_territory.py"


rule fig5:
    """Figure 5 parts"""
    input: rules.plot_chromosome_territory.output[0]
    output: touch("../output/paper_submission/figures/fig5.done")


################################################################################
# Figure 6: Pol II
################################################################################
rule plot_polII:
    input: "../output/polII-wf/s2_and_s5_boxplot.svg"
    output: "../output/paper_submission/figures/polII.svg"
    shell: "cp {input[0]} {output[0]}"


rule fig6:
    """Figure 6 parts"""
    input: rules.plot_polII.output[0]
    output: touch("../output/paper_submission/figures/fig6.done")


####################################
# Table S1: bulk data
####################################
## Normalized Data (L3 Testis, L3 Ovary, Adult Testis, Adult Ovary)
## Correlation (Bulk vs Single-Cell)
rule larval_bulk_gene_level:
    input: "../output/bulk-rnaseq-wf/rnaseq_aggregation/gene_level_counts.tsv"
    output: '../output/paper_submission/larval_bulk_RNA-Seq_gene_level_counts.tsv.gz'
    run:
        (
            pd.read_csv(input[0], sep="\t")
            .set_index("Geneid")
            .rename_axis("FBgn")
            .reset_index()
            .merge(FBGN2CHROM, on="FBgn", how="left")
            .set_index(["FBgn", "gene_symbol", "Chromosome"])
            .to_csv(output[0], sep="\t")
        )


rule adult_bulk_gene_level:
    input: 
        data="../output/expression-atlas-wf/w1118_gene_counts.feather",
        gene_annot="../references/gene_annotation_dmel_r6-26.feather"
    output: '../output/paper_submission/adult_bulk_RNA-Seq_gene_level_counts.tsv.gz'
    run: 
        (
            pd.pivot_table(
                pd.read_feather(input[0], columns=["FBgn", "sample_ID", "Count"]),
                index="FBgn",
                columns="sample_ID",
                values="Count"
            )
            .reset_index()
            .merge(FBGN2CHROM, on="FBgn", how="left")
            .set_index(["FBgn", "gene_symbol", "Chromosome"])
            .to_csv(output[0], sep="\t")
        )
        

rule spearman_corr_bulk_vs_sum_single_cell:
    input:
        gene_annot="../references/gene_annotation_dmel_r6-26.feather",
        larval_scrnaseq="../output/seurat3-cluster-wf/aggegated_gene_counts.feather",
        larval_bulk="../output/bulk-rnaseq-wf/rnaseq_aggregation/tpm_gene_level_counts.tsv",
    output: "../output/paper_submission/spearman_corr_bulk_vs_sum_single_cell.tsv"
    script: "scripts/correlation_table_bulk_sum_single_cell.py"


rule spearman_corr_bulk_vs_sum_single_cell_pvals:
    input:
        gene_annot="../references/gene_annotation_dmel_r6-26.feather",
        larval_scrnaseq="../output/seurat3-cluster-wf/aggegated_gene_counts.feather",
        larval_bulk="../output/bulk-rnaseq-wf/rnaseq_aggregation/tpm_gene_level_counts.tsv",
    output: "../output/paper_submission/spearman_corr_bulk_vs_sum_single_cell_pvals.tsv"
    script: "scripts/correlation_table_bulk_sum_single_cell_pvals.py"


rule spearman_corr_bulk_vs_single_cell_clusters:
    input:
        gene_annot="../references/gene_annotation_dmel_r6-26.feather",
        larval_scrnaseq="../output/seurat3-cluster-wf/tpm_by_cluster_rep.feather",
        larval_bulk="../output/bulk-rnaseq-wf/rnaseq_aggregation/tpm_gene_level_counts.tsv",
        adult_bulk="../output/expression-atlas-wf/w1118_gene_counts.feather",
    output: "../output/paper_submission/spearman_corr_bulk_vs_single_cell_clusters.tsv"
    script: "scripts/correlation_table_bulk_vs_single_cell_clusters.py"


rule table_s1:
    input:
        larval=rules.larval_bulk_gene_level.output[0],
        adult=rules.adult_bulk_gene_level.output[0],
        corr_reps=rules.spearman_corr_bulk_vs_sum_single_cell.output[0],
        corr_clusters=rules.spearman_corr_bulk_vs_single_cell_clusters.output[0]
    output: "../output/paper_submission/TableS1.xlsx"
    params:
        cluster_order=config["cluster_order"]
    script: "scripts/table_s1.py"

####################################
# Table S2: Single-cell RNA-Seq Data
####################################
rule cell_metadata:
    input: 
        metadata="../output/cellselection-wf/cell_metadata.feather",
        clusters="../output/seurat3-cluster-wf/combined_n3_clusters.feather",
        cell_calls=expand("../output/cellselection-wf/{sample}_combined_cell_calls.feather", sample=["testis1", "testis2", "testis3"]),
        scrublets=expand("../output/cellselection-wf/{sample}_scrublet_dublets.txt", sample=["testis1", "testis2", "testis3"]),
        umap="../output/seurat3-cluster-wf/combined_n3_umap.feather",
        autosome_ratios_all_genes="../output/x-to-a-wf/autosome_ratios_expressed_by_cell.feather",
        autosome_ratios_commonly_expressed_genes="../output/x-to-a-wf/autosome_ratios_commonly_expressed_by_cell.feather",
    output: "../output/paper_submission/cell_metadata.tsv"
    script: "scripts/cell_metadata_table.py"

rule gene_metadata:
    input: 
        gene_annot="../references/gene_annotation_dmel_r6-26.feather",
        biomarkers="../output/seurat3-cluster-wf/combined_n3_biomarkers.feather",
        raw="../output/seurat3-cluster-wf/raw_by_cluster_rep.feather",
        tpm="../output/seurat3-cluster-wf/tpm_by_cluster_rep.feather",
        zscore="../output/seurat3-cluster-wf/zscore_by_cluster_rep.feather",
        tau="../output/expression-atlas-wf/tau_housekeeping/male_fbgns.pkl",
        tsps="../output/expression-atlas-wf/tsps_housekeeping/male_fbgns.pkl",
    params: 
        cluster_annot=config['cluster_annot'],
        cluster_order=config['cluster_order'],
    output: "../output/paper_submission/gene_metadata.tsv"
    script: "scripts/gene_metadata_table.py"

rule cluster_metadata:
    input:
        clusters="../output/seurat3-cluster-wf/combined_n3_clusters.feather",
        tpm="../output/seurat3-cluster-wf/tpm_by_cluster.feather",
    params:
        cluster_annot=config["cluster_annot"],
        cluster_order=config["cluster_order"],
        legend_names=config["legend_names"],
    output: "../output/paper_submission/cluster_metadata.tsv"
    script: "scripts/cluster_metadata_table.py"

rule biomarkers_to_tsv:
    input: "../output/seurat3-cluster-wf/combined_n3_biomarkers.feather"
    output: "../output/paper_submission/one_vs_rest.tsv"
    params: cluster_annot = config["cluster_annot"]
    run:
        (
            pd.read_feather(input[0])
            .merge(FBGN2CHROM, on=["FBgn", "gene_symbol"], how="left")
            .set_index(["FBgn", "gene_symbol", "Chromosome"])
            .assign(cluster=lambda x: x.cluster.map(lambda y: params.cluster_annot[y]))
            .to_csv(output[0], sep="\t")
        )

rule table_s2:
    input: 
        cell=rules.cell_metadata.output[0],
        gene=rules.gene_metadata.output[0],
        cluster=rules.cluster_metadata.output[0],
        biomarkers=rules.biomarkers_to_tsv.output[0],
    output: "../output/paper_submission/TableS2.xlsx"
    script: "scripts/table_s2.py"


####################################
# Table S3: Literature Genes
####################################
# This is done by hand

####################################
# Table S4: ART Table
####################################
# This is done by hand

################################################################################
# Files
################################################################################
rule raw_to_tsv:
    input: '../output/cellselection-wf/raw.feather'
    output: '../output/paper_submission/scRNAseq_raw_counts.tsv.gz'
    run:
        (
            pd.read_feather(input[0])
            .merge(FBGN2CHROM, on="FBgn", how="left")
            .set_index(["FBgn", "gene_symbol", "Chromosome"])
            .to_csv(output[0], sep="\t")
        )


rule seurat_normalized_to_tsv:
    input: '../output/seurat3-cluster-wf/combined_n3_normalized.feather'
    output: '../output/paper_submission/scRNAseq_seurat_normalized_counts.tsv.gz'
    run:
        (
            pd.read_feather(input[0])
            .merge(FBGN2CHROM, on=["FBgn", "gene_symbol"], how="left")
            .set_index(["FBgn", "gene_symbol", "Chromosome"])
            .to_csv(output[0], sep="\t")
        )


rule plot_umaps_all_genes:
    input:
        gene_annot=f"../references/gene_annotation_dmel_{TAG}.feather",
        clusters="../output/seurat3-cluster-wf/combined_n3_clusters.feather",
        zscores="../output/seurat3-cluster-wf/zscore_by_cell.feather",
        umap="../output/seurat3-cluster-wf/combined_n3_umap.feather",
        norm="../output/seurat3-cluster-wf/combined_n3_normalized.feather"
    output: "../output/paper_submission/figures/projections_all_genes/{fbgn}_{symbol}.svg"
    script: "scripts/plot_umap_projections.py"


################################################################################
# Deprecated
################################################################################
rule plot_lit_gene_evidence:
    input:
         gene_metadata = f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather",
         lit_evidence = '../data/external/miriam/lit_gene_dummy_vars.tsv',
         zscore_by_cluster_rep="../output/seurat3-cluster-wf/zscore_by_cluster_rep.feather"
    output: "../output/paper_submission/figures/lit-gene-evidence.svg"
    params: cmap=COLORS["heatmap"]
    script: "scripts/plot_lit_gene_evidence.py"


rule plot_lit_gene_evidence_soma:
    input:
         gene_metadata = f"../references/gene_annotation_{ASSEMBLY}_{TAG}.feather",
         lit_evidence = '../data/external/miriam/lit_gene_dummy_vars.tsv',
         zscore_by_cluster_rep="../output/seurat3-cluster-wf/zscore_by_cluster_rep.feather"
    output: "../output/paper_submission/figures/lit-gene-evidence_soma.svg"
    params: cmap=COLORS["heatmap"]
    script: "scripts/plot_lit_gene_evidence_soma.py"

rule deg_to_tsv:
    """ This rule builds germline DEG comparisons.
        Files that it is used to create:
            '../output/paper_submission/adult_bulk_RNA-Seq_gene_level_counts.tsv.gz',
            '../output/paper_submission/larval_bulk_RNA-Seq_gene_level_counts.tsv.gz',
            '../output/paper_submission/larval_bulk_RNA-Seq_intergenic_counts.tsv.gz',
    """
    input: "../output/seurat3-cluster-wf/germline_deg/{comparison}.feather"
    output: "../output/paper_submission/{comparison}.tsv"
    run:
        (
            pd.read_feather(input[0])
            .merge(FBGN2CHROM, on=["FBgn", "gene_symbol"], how="left")
            .set_index(["FBgn", "gene_symbol", "Chromosome"])
            .to_csv(output[0], sep="\t")
        )

