"""Look for changes in X to Autosome ratios.

There is some debate about if the X is meiotically silenced during
spermiogenesis. Since we have cell type resolution we look at the patterns of
X, 4th, and Y expression during germline development. This workflow calculate 
autosome ratios at the individual cell level. We then use a permutation test
to determine if ratios are significantly different across cell types.

"""
import os
import yaml
from pathlib import Path

import pandas as pd

from larval_gonad.config import read_config
from larval_gonad.io import pickle_load

workdir: '.'
configfile: '../config/common.yaml'

REFERENCES_DIR = os.environ.get('REFERENCES_DIR', config.get('REFERENCES_DIR', ''))
ASSEMBLY = config['assembly']
TAG = config['tag']
CHROMS = config['chrom_order']
AUTOSOMES = config['autosomes']


rule targets:
    input:
        expand(
            '../output/x-to-a-wf/db/{fbgns}.dat',
            fbgns=config['gene_subsets']
        )


rule fbgn2chrom:
    input: f'../references/gene_annotation_{ASSEMBLY}_{TAG}.feather'
    output: "../output/x-to-a-wf/fbgn2chrom.pkl"
    run:
        (
            pd.read_feather(input[0], columns=['FBgn', 'FB_chrom'])
            .set_index('FBgn')
            .rename({'FB_chrom': 'chrom'}, axis=1)
            .to_pickle(output[0])
        )


def _subset_of_genes_to_use(wildcards):
    return {
        'expressed': '../output/cellselection-wf/expressed_genes.pkl',
        'commonly_expressed': '../output/cellselection-wf/commonly_expressed_genes.pkl',
        'tau': '../output/expression-atlas-wf/dmel_male_tau_fbgns.pkl',
        'tsps': '../output/expression-atlas-wf/dmel_male_tsps_fbgns.pkl',
        'male': '../output/expression-atlas-wf/dmel_male_sex_biased_fbgns.pkl',
        'female': '../output/expression-atlas-wf/dmel_female_sex_biased_fbgns.pkl',
        'sex_ns': '../output/expression-atlas-wf/dmel_ns_sex_biased_fbgns.pkl',
        'testis': '../output/expression-atlas-wf/dmel_testis_gonad_biased_fbgns.pkl',
        'ovary': '../output/expression-atlas-wf/dmel_ovary_gonad_biased_fbgns.pkl',
        'gonad_ns': '../output/expression-atlas-wf/dmel_ns_gonad_biased_fbgns.pkl',
        'male_soma': '../output/expression-atlas-wf/dmel_male_sex_biased_fbgns.pkl',
        'female_soma': '../output/expression-atlas-wf/dmel_female_sex_biased_fbgns.pkl',
        'sex_soma_ns': '../output/expression-atlas-wf/dmel_ns_sex_biased_fbgns.pkl',
        'larval_testis': '../output/bulk2-rnaseq-wf/deg/larval_testis_biased_fbgns.pkl',
        'larval_ovary': '../output/bulk2-rnaseq-wf/deg/larva_ovary_biased_fbgns.pkl',
        'larval_gonad_ns': '../output/bulk2-rnaseq-wf/deg/larval_ns_biased_fbgns.pkl',
    }[wildcards.fbgns]


rule autosome_ratios_by_cell:
    input:
        raw = '../output/cellselection-wf/raw.feather',
        fbgn2chrom = rules.fbgn2chrom.output[0],
        clusters = '../output/seurat3-cluster-wf/combined_n3_clusters.feather',
        target_fbgns = _subset_of_genes_to_use
    params:
        autosomes = config['autosomes'],
        chrom_order = config['chrom_order']
    output: '../output/x-to-a-wf/autosome_ratios_{fbgns}_by_cell.feather'
    script: "scripts/autosome_ratios_by_cell.py"


rule permutation_test_autosome_ratios:
    input: rules.autosome_ratios_by_cell.output[0]
    output: '../output/x-to-a-wf/permuted_autosome_ratio_pvalues_{fbgns}.feather'
    script: 'scripts/permutation_test_autosome_ratios.py'


rule shelve_results:
    input: 
        ratios = rules.autosome_ratios_by_cell.output[0],
        pvalues = rules.permutation_test_autosome_ratios.output[0]
    params: '../output/x-to-a-wf/db/{fbgns}',
    output: 
        '../output/x-to-a-wf/db/{fbgns}.dat',
        '../output/x-to-a-wf/db/{fbgns}.dir',
        '../output/x-to-a-wf/db/{fbgns}.bak'
    script: 'scripts/shelve_results.py'